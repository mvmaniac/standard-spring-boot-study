plugins {
  id 'org.springframework.boot' version '2.5.2'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id "com.github.node-gradle.node" version "2.2.4"
  id 'java'
}

group = 'io.devfactory'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

// querydsl 적용
//def querydslSrcDir = "src/main/generated" // intellij로 설정되어 있다면
def querydslSrcDir = "build/generated/sources/annotationProcessor/java/main" // gradle로 설정되어 있다면

sourceSets {
  main.java.srcDirs += [querydslSrcDir]
}

tasks.withType(JavaCompile) {
  options.annotationProcessorGeneratedSourcesDirectory = file(querydslSrcDir)
}

clean.doLast() {
  file(querydslSrcDir).deleteDir()
}

node {
  download = false
  nodeModulesDir = file("${project.projectDir}/src/main/resources/static")
}

compileJava {
  dependsOn npmInstall
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework.boot:spring-boot-starter-mail'

  implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
  implementation 'org.modelmapper:modelmapper:2.4.3'
  implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.6.2'

  implementation 'com.querydsl:querydsl-jpa'
  annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa"
  // querydsl JPAAnnotationProcessor 사용 지정
  annotationProcessor 'jakarta.persistence:jakarta.persistence-api:2.2.3'
  annotationProcessor 'jakarta.annotation:jakarta.annotation-api:1.3.5'

  implementation 'org.webjars:jquery:3.5.1'
  implementation 'org.webjars:bootstrap:4.5.0'
  implementation 'org.webjars:font-awesome:5.13.0'

  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

  runtimeOnly 'com.h2database:h2'
  runtimeOnly 'org.postgresql:postgresql'

  developmentOnly 'org.springframework.boot:spring-boot-devtools'

  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'

  testCompileOnly 'org.projectlombok:lombok'
  testAnnotationProcessor 'org.projectlombok:lombok'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation 'com.tngtech.archunit:archunit-junit5:0.14.1'
  testImplementation 'org.testcontainers:junit-jupiter:1.14.3'
  testImplementation 'org.testcontainers:postgresql:1.14.3'
}

test {
  useJUnitPlatform()
}
